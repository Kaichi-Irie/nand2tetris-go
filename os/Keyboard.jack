// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack
/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static int baseAddress;
    /** Initializes the keyboard. */
    function void init() {
        let baseAddress = 24576; // The base address for keyboard input
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(baseAddress);
    }

    /**	Waits until a key is pressed on the keyboard and released,
     *  then echoes the key to the screen, and returns the character
     *  of the pressed key. */
    function char readChar() {
        var char c;
        while (Keyboard.keyPressed() = 0) {
            // Wait for a key to be pressed
        }
        let c = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed() = 0)) {
            // Wait for the key to be released
        }
        do Output.printChar(c);
        return c;
    }

    /**	Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its value. Also handles user backspaces. */
    function String readLine(String message) {
        var String inputText;
        var char c;
        let inputText = String.new(100); // Create a new string with a max length of 100 characters
        do Output.printString(message);
        while (true) {
            let c = Keyboard.readChar();
            if (c = 128) { // Newline character
                do Output.println();
                return inputText;
            }
            if (c = 129) { // Backspace character
                do Output.backSpace();
                if (inputText.length() > 0) {
                    do inputText.eraseLastChar();
                }
            } else {
                do inputText.appendChar(c);
            }
        }
        return inputText;
    }

    /** Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its integer value (until the first non-digit character in the
     *  entered text is detected). Also handles user backspaces. */
    function int readInt(String message) {
        var String inputText;
        var int value;
        let inputText = Keyboard.readLine(message);
        let value = inputText.intValue(); // Initialize value
        return value;
    }
}
